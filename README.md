
# Структура переопределяемых обработчиков объектов и фор
### Краткое описание
Структура переопределяемых обработчиков объектов и форм.

### Описание
Подход для структурирования переопределяемых обработчикок объектов и форм.
Идея в том, чтобы для каждого объекта или формы вынести все переопределеяемые обработчики в отдельные общие модули, для каждого объекта/формы свой модуль, имя модуля конструируется по определенному шаблону, так чтобы, по имени объекта или формы можно было программно получить имя модуля. Это позволит не привязываться к конкретным именам и типам в коде, и все обработчики вызывать единообразно. Так же в модуле располагается метод, который возвращает настройку выполнения того или иного обработчика.

Например, для форм предлагается использовать шаблон **ПрефиксКонфигурации_Форма_ТипМетаданных_ИмяМетаданных_ИмяФормы**, для формы "Справочник.Номенклатура.Форма.ФормаЭлемента", имя модуля будет - "мк_Форма_Справочник_Номенклатура_ФормаЭлемента".
Для объектов - **ПрефиксКонфигурации_ТипМетаданных_ИмяМетаданных**, для справочника "Номенклатура", имя модуля будет - "мк_Справочник_Номенклатура".

Обработчики форм переопределяем в модуле **МодификацияКонфигурацииПереопределяемый**, для объектов обработчики через подписки.
Методы обработчиков подписок на события справочников расположены в модуле **мк_СобытияСправочников**.

Вызывается обработчик подписки на событие **мк_СобытияСправочников.ПередЗаписью**

Общий обработчик объекта: 
```bsl
Процедура ПередЗаписью(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Определение общего модуля объекта.
	Модуль = ОбщийМодульОбъекта(Источник);

	// Параметры обработчиков.
	ПараметрыОбработчиков = ПараметрыОбработчиковОбъекта(Модуль);
		
	Если ПараметрыОбработчиков.ПередЗаписью Тогда
		Модуль.ПередЗаписью(Источник, Отказ);	
	КонецЕсли;	
	
КонецПроцедуры
```

Определение общего модуля:
```bsl
Функция ОбщийМодульОбъекта(Источник)
	
	ТипИсточника = ТипЗнч(Источник);
	
	ИмяОбъекта = Метаданные.НайтиПоТипу(ТипИсточника).ПолноеИмя();
	
	Префикс = мк_ОбщегоНазначения.ПрефиксИмен();
	
	ИмяОбъекта = СтрЗаменить(ИмяОбъекта, ".", "_");
	
	ИмяМодуля = СтрШаблон("%1%2", Префикс, ИмяОбъекта);
	
	Модуль = ОбщийМодуль(ИмяМодуля);
	
	Возврат Модуль;

КонецФункции 
```
Определение параметров обработчиков объекта:
```bsl
Функция ПараметрыОбработчиковОбъекта(Модуль)

	ПараметрыОбработчиков = ПараметрыОбработчиковПоУмолчанию();
	
	Если Модуль = Неопределено Тогда
		Возврат ПараметрыОбработчиков;
	КонецЕсли;	
		
	Модуль.ОпределитьПараметрыОбработчиков(ПараметрыОбработчиков);
	
	Возврат ПараметрыОбработчиков; 

КонецФункции

// Инициализирует набор параметров, задающих флаги выполнения обработчиков.
//
// Возвращаемое значение:
//   Структура   - Обработчики, переопределенные для объекта.
//
Функция ПараметрыОбработчиковПоУмолчанию()
	
	ПараметрыОбработчиков = Новый Структура;
	
	ПараметрыОбработчиков.Вставить("ПередЗаписью", Ложь);
	ПараметрыОбработчиков.Вставить("ПриЗаписи", Ложь);
	
	ПараметрыОбработчиков.Вставить("ПередУдалением", Ложь);
	
	ПараметрыОбработчиков.Вставить("ОбработкаЗаполнения", Ложь);
	ПараметрыОбработчиков.Вставить("ОбработкаПроверкиЗаполнения", Ложь);
	
	ПараметрыОбработчиков.Вставить("ОбработкаПолученияПредставления", Ложь);
	ПараметрыОбработчиков.Вставить("ОбработкаПолученияФормы", Ложь);

	Возврат ПараметрыОбработчиков;

КонецФункции
```

Пример для справочника "Номенклатура".
Модуль **мк_Справочник_Номенклатура**
```bsl
// Инициализирует набор параметров, задающих флаги выполнения обработчиков.
//
// Параметры:
//  ПараметрыОбработчиков	 - Структура - Параметры обработчиков.
//
Процедура ОпределитьПараметрыОбработчиков(ПараметрыОбработчиков) Экспорт

	ПараметрыОбработчиков.Вставить("ПередЗаписью", Истина);

КонецПроцедуры

// Процедура - обработчик подписки на событие "ПередЗаписью".
//	См. "СправочникОбъект.ПередЗаписью" в синтакс-помощнике.
//
// Параметры:
//  Источник  - СправочникОбъект - Источник события.
//  Отказ  - Булево - Флаг отмены.
//
Процедура ПередЗаписью(Источник, Отказ) Экспорт
	
	ЗаполнитьИдентификатовAD(Источник);

КонецПроцедуры
```

Какие преимущества дает такой подход:
- Простота подключения новых объектов и новых обработчиков.
- Код получается более лучше структурирован.
- Меньше конфликтов при совместной разработке.

### Ключевые слова
Обработчики, адаптация